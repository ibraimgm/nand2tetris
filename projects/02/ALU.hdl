// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zx and nx flags.
    // final result is rx
    Mux16(a=x, b=false, sel=zx, out=zxr);
    Not16(in=zxr, out=notx);
    Mux16(a=zxr, b=notx, sel=nx, out=rx);

    // zy and ny flags
    // result is ry
    Mux16(a=y, b=false, sel=zy, out=zyr);
    Not16(in=zyr, out=noty);
    Mux16(a=zyr, b=noty, sel=ny, out=ry);

    // f flag
    // result is in rout
    Add16(a=rx, b=ry, out=add);
    And16(a=rx, b=ry, out=and);
    Mux16(a=and, b=add, sel=f, out=rout);

    // no flag.
    // result is outlsb, outmsb and sign
    Not16(in=rout, out=notrout);
    Mux16(a=rout, b=notrout, sel=no, out[0..7]=outlsb, out[8..14]=outmsb, out[15]=sign);
    And16(a[0..7]=outlsb, a[8..14]=outmsb, a[15]=sign, b=true, out=out); // dummy, hust to set final output

    // setting zr
    Or8Way(in=outlsb, out=lsbOr);
    Or8Way(in[0..6]=outmsb, in[7]=sign, out=msbOr);
    Or(a=lsbOr, b=msbOr, out=or16w);
    Not(in=or16w, out=zr);

    // setting ng
    Or(a=sign, b=false, out=ng);
}
