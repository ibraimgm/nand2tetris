// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // increment the 'old' value, if applicable
    Inc16(in=oldOutput, out=plusOne);
    Mux16(a=oldOutput, b=plusOne, sel=inc, out=outputInc);

    // load a new value, if applicable
    Mux16(a=outputInc, b=in, sel=load, out=loadOut);

    // reset to zero, if applicable
    Mux16(a=loadOut, b=false, sel=reset, out=newInput);

    // save the changes
    Or(a=load, b=inc, out=loadOrInc);
    Or(a=loadOrInc, b=reset, out=saveChanges);
    Register(in=newInput, load=saveChanges, out=oldOutput);

    // hdl hack
    Or16(a=oldOutput, b=false, out=out);
}
